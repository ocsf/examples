
root.activity_id = 6
root.activity_name = "Traffic"
root.category_uid = 4
root.category_name = "Network Activity"
root.class_uid = 4001
root.class_name = "Network Activity"
#root.type_uid = 400106
#root.type_name = "Network Activity: Traffic"
root.time = this.winlog.event_data.UtcTime.ts_strptime("%Y-%m-%d %H:%M:%S.%f").ts_unix_milli()
root.time_dt = this.winlog.event_data.UtcTime
root.severity_id = 1
root.severity = "Informational"
root.status_id = 1
root.message = this.winlog.task
root.disposition_id = 17
root.disposition = "Logged"

root.metadata.profiles = ["host", "security_control","datetime"]
root.metadata.event_code = this.winlog.event_id
root.metadata.uid = this.winlog.record_id.string()
root.metadata.logged_time = this.event.created.ts_unix_milli()
root.metadata.logged_time_dt = this.event.created
root.metadata.log_name = this.winlog.channel
root.metadata.log_provider = this.winlog.provider_name
root.metadata.product.lang = "EN"
root.metadata.product.name = "Sysmon"
root.metadata.product.vendor_name = "Sysmon"
root.metadata.version = "1.0.0"
root.metadata.original_time = this.winlog.event_data.UtcTime

root.device.type = "Server"
root.device.type_id = 1
root.device.hostname = this.winlog.computer_name.split(".").index(0)
root.device.os.type_id = 100
root.device.os.type = "Windows"
root.device.os.name = "Microsoft Windows"

root.actor.process.file.name = this.winlog.event_data.Image.split("\\").index(-1)
root.actor.process.file.path = this.winlog.event_data.Image
root.actor.process.file.type_id = match {
    this.winlog.event_data.Image.contains(".") => 1
    _=> 99
}
root.actor.process.file.type = match {
    this.winlog.event_data.Image.contains(".") => "Regular File"
    _=> "Other"
}
root.actor.process.pid = this.winlog.event_data.ProcessId.number()
root.actor.process.uid = this.winlog.event_data.ProcessGuid.trim("{}")
root.actor.process.user.name = this.winlog.event_data.User.split("\\").index(-1)
root.actor.process.user.domain = this.winlog.event_data.User.split("\\").index(0)
root.actor.process.name = this.winlog.event_data.Image.split("\\").index(-1)
root.actor.process.user.type_id = match this.winlog.event_data.User {
    this.contains("NT AUTHORITY") => 3
    this.contains("S-1-5-18") => 3
    this.contains("Administrator") => 2
    this.contains("my_id_for_my_company") => 1
    _=> 0
}
root.actor.process.user.type = match this.winlog.event_data.User {
    this.contains("NT AUTHORITY") => "System"
    this.contains("S-1-5-18") => "System"
    this.contains("Administrator") => "Admin"
    this.contains("my_id_for_my_company") => "User"
    _=> 0
}

root.src_endpoint.hostname = this.winlog.computer_name
root.src_endpoint.ip = this.winlog.event_data.SourceIp
root.src_endpoint.port = this.winlog.event_data.SourcePort.number()
root.src_endpoint.domain = this.winlog.event_data.User.split("\\").index(0)

root.dst_endpoint.hostname = this.winlog.event_data.DestinationHostname
root.dst_endpoint.ip = this.winlog.event_data.DestinationIp
root.dst_endpoint.port = this.winlog.event_data.DestinationPort.number()

root.connection_info.protocol_ver = match {
    this.winlog.event_data.SourceIsIpv6.contains("true") => "IPv6"
    this.winlog.event_data.SourceIsIpv6.contains("false") => "IPv4"
}
root.connection_info.direction_id = 0
root.connection_info.direction = "Unknown"
root.connection_info.protocol_num = match {
    this.winlog.event_data.Protocol.contains("tcp") => 6
    this.winlog.event_data.Protocol.contains("udp") => 17
    this.winlog.event_data.Protocol.contains("icmp") => 1
}
root.connection_info.protocol_name = match {
    this.winlog.event_data.Protocol.contains("tcp") => "TCP"
    this.winlog.event_data.Protocol.contains("udp") => "UDP"
    this.winlog.event_data.Protocol.contains("icmp") => "ICMP"
}

root.attacks = [{
    "version": "v13",
    "technique": {
        "name" : this.mitre.technique_name,
        "uid" : this.mitre.technique_id},
    "tactics" : [],
}]

root.type_uid = root.class_uid.number() * 100 + root.activity_id.number()
root.unmapped = this.without(
    "winlog.record_id",
    "winlog.event_data.Image",
    "winlog.event_data.SourceIsIpv6",
    "winlog.event_data.Protocol",
    "winlog.event_data.DestinationHostname",
    "winlog.event_data.DestinationIp",
    "winlog.event_data.DestinationPort",
    "winlog.channel",
    "winlog.provider_name",
    "winlog.computer_name",
    "winlog.event_data.SourceIp",
    "winlog.event_data.SourcePort",
    "winlog.event_data.User",
    "winlog.event_data.UtcTime",
    "winlog.task",
    "winlog.event_data.ProcessId",
    "winlog.event_data.ProcessGuid",
    "winlog.event_id",
    "mitre.technique_id",
    "mitre.technique_name",
    "event.created"
)


observables = root.without("unmapped").with("device.hostname", "dst_endpoint.ip", "src_endpoint.ip", "actor.process.user.name", "actor.process.file.name", "actor.process.name").(item -> {
   "a": match {item.device.exists("hostname") => {"name": "device.hostname", "type": "Hostname", "type_id": 1, "value": item.device.hostname}},
   "b": match {item.dst_endpoint.exists("ip") => {"name": "dst_endpoint.ip", "type": "IP Address", "type_id": 2, "value": item.dst_endpoint.ip}},
   "c": match {item.src_endpoint.exists("ip") => {"name": "src_endpoint.ip", "type": "IP Address", "type_id": 2, "value": item.src_endpoint.ip}},
   "d": match {item.actor.process.user.exists("name") => {"name": "actor.process.user.name", "type": "User Name", "type_id": 4, "value": item.actor.process.user.name}},
   "e": match {item.actor.process.file.exists("name") => {"name": "actor.process.file.name", "type": "File Name", "type_id": 7, "value": item.actor.process.file.name}},
   "f": match {item.actor.process.exists("name") => {"name": "actor.process.name", "type": "Process Name", "type_id": 9, "value": item.actor.process.name}},
   }).values()